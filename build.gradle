buildscript {
    ext.kotlin_version = '1.3.21'

    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.45"
    }
}

// apply kotlin2js
apply plugin: "kotlin2js"

//// apply plugin
//apply plugin: "org.jetbrains.kotlin.frontend"

group 'info.spiralframework'
version '0.1'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js"
    testImplementation "org.jetbrains.kotlin:kotlin-test-js"
}

// configure kotlin compiler
compileKotlin2Js {
    kotlinOptions.metaInfo = true
    //kotlinOptions.outputFile = "${project.buildDir.path}/js/${project.name}.js"
    kotlinOptions.sourceMap = true
    //kotlinOptions.moduleKind = 'amd'
    kotlinOptions.main = "call"
}

task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/web/assets/kts"

    dependsOn classes
}

assemble.dependsOn assembleWeb

sourceSets.main.kotlin.srcDirs = ["kts/src/main/kotlin"]
sourceSets.main.resources.srcDirs = ["kts/src/main/resources"]
sourceSets.test.kotlin.srcDirs = ["kts/src/test/kotlin"]
sourceSets.test.resources.srcDirs = ["kts/src/test/resources"]